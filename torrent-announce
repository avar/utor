#!/usr/bin/env perl

use strict;
use warnings;
use DateTime;
use DateTime::Duration;
use DateTime::Format::Human::Duration;
use File::Basename qw(fileparse);
use Fcntl ':flock';
use POE;
use POE::Component::IRC::Common qw(parse_user NORMAL DARK_GREEN DARK_BLUE ORANGE TEAL);
use POE::Component::IRC::Plugin::Connector;
use POE::Component::IRC::Plugin::AutoJoin;
use POE::Component::IRC::Plugin::FollowTail;
use POE::Component::IRC::State;
use FindBin;
use File::Spec::Functions qw(catfile);
use YAML::Any qw(LoadFile);
use Format::Human::Bytes;

# only run one instance of the IRC bot at a time
open my $self, '<', $0 or die "Couldn't open self: $!";
flock $self, LOCK_EX | LOCK_NB or die "This script is already running";

my $config = LoadFile(catfile($FindBin::Bin, 'utor.yml'));
die 'No IRC server specified' if !defined $config->{irc_server};
die 'No log file specified' if !defined $config->{log_file};

if (!-e $config->{log_file}) {
    open my $log_handle, '>>', $config->{log_file}
        or die "Couldn't create log file $config->{log_file}";
    close $log_handle;
}

POE::Session->create(
    package_states => [
        (__PACKAGE__) => [qw(_start announce interrupt inserted_new_torrent finished_torrent unrar_torrent irc_tail_input)],
    ],
);

$poe_kernel->run();

sub _start {
    my ($kernel, $session, $heap) = @_[KERNEL, SESSION, HEAP];

    $kernel->sig(INT => 'interrupt');

    $heap->{irc} = POE::Component::IRC::State->spawn(
        nick         => $config->{irc_nick},
        password     => $config->{irc_pass},
        server       => $config->{irc_server},
        port         => $config->{irc_port},
        debug        => 1,
        plugin_debug => 1,
    );

    if ($config->{irc_channel}) {
        $heap->{irc}->plugin_add('AutoJoin', POE::Component::IRC::Plugin::AutoJoin->new(
            Channels => [$config->{irc_channel}],
        ));
    }
        
    $heap->{irc}->plugin_add('Connector', POE::Component::IRC::Plugin::Connector->new());
    $heap->{irc}->plugin_add('FollowTail', POE::Component::IRC::Plugin::FollowTail->new(
        filename => '/tmp/torrentlog',
    ));

    $heap->{irc}->yield('connect');
}

sub interrupt {
    $_[HEAP]->{irc}->yield('shutdown');
}

sub irc_tail_input {
    my ($action, $args) = split /,/, $_[ARG1], 2;
    $_[KERNEL]->yield("${action}_torrent" => $args);
}

sub inserted_new_torrent {
    my (undef, $hash, $bytes, undef, $file) = split /,/, $_[ARG0], 5;
    return if !defined $hash || !defined $bytes || !defined $file;

    my $name = fileparse($file);
    $name =~ s/\.torrent$//;
    my $user = getpwuid((stat $file)[4]);

    my $size = fmt_bytes($bytes);

    my $msg = DARK_BLUE.'Enqueued: '.ORANGE.$name.NORMAL." ($size, by $user)";
    $poe_kernel->yield(announce => $msg);
}

sub finished_torrent {
    my ($finished, $hash, $bytes, $rars, $file) = split /,/, $_[ARG0], 5;
    return if !defined $finished || !defined $hash || !defined $bytes || !defined $rars || !defined $file;

    my $enqueued = (stat $file)[10];
    my $duration = _duration($enqueued, $finished);
    my $name = fileparse($file);
    $name =~ s/\.torrent$//;

    my $secs = $finished - $enqueued;
    # Sometimes it's that fast, avoid division by zero
    $secs = 1 if $secs == 0;

    my $bps = $bytes / $secs;
    my $size = fmt_bytes($bps);
    my $rate = "$size/s";

    my $msg = DARK_GREEN.'Finished: '.ORANGE.$name.NORMAL." in $duration ($rate)";
    my $archives = $rars > 1 ? 'archives' : 'archive';
    $msg .= "; $rars $archives to unrar" if $rars;
    $poe_kernel->yield(announce => $msg);
}

sub unrar_torrent {
    my ($start, $finish, $rars, $file) = split /,/, $_[ARG0], 5;
    return if !defined $start || !defined $finish || !defined $rars || !defined $file;

    my $duration = _duration($start, $finish);
    my $name = fileparse($file);
    $name =~ s/\.torrent$//;

    my $archives = $rars > 1 ? 'archives' : 'archive';
    my $msg = TEAL.'Unrared: '.ORANGE.$name.NORMAL." in $duration ($rars $archives)";
    $poe_kernel->yield(announce => $msg);
}

sub announce {
    my ($heap, $msg) = @_[HEAP, ARG0];
    my $irc = $heap->{irc};
    my $channels = $irc->channels();
    $irc->yield(notice => $_, $msg) for keys %$channels;
}

sub _duration {
    my ($start, $finish) = @_;

    my $enq_date = DateTime->from_epoch(epoch => $start);
    my $fin_date = DateTime->from_epoch(epoch => $finish);
    my $dur_obj = $fin_date - $enq_date;
    my $span = DateTime::Format::Human::Duration->new();
    return $span->format_duration($dur_obj);
}

sub fmt_bytes { Format::Human::Bytes::base2( $_[0] ) }

=head1 NAME

torrent-announce - An IRC bot that announces when torrents are enqueued,
finished, and unrared

=head1 AUTHOR

Hinrik Örn Sigurðsson <hinrik.sig@gmail.com>
