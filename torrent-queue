#!/usr/bin/env perl
use strict;
use warnings;
use File::Spec::Functions 'catfile';
use FindBin;
use List::Util 'first';
use YAML::Any 'LoadFile';

my $config = LoadFile(catfile($FindBin::Bin, 'utor.yml'));

my ($action, $hash, $size, $torrent, $content) = @ARGV;
my @log_values = ($action, time, $hash, $size, 0, $torrent);

if ($action eq 'inserted_new') {
    torrentlog(@log_values);
}
elsif ($action eq 'finished') {
    chdir $config->{complete_dir};
    my @rars;

    if (-d $content) {
        push @rars, find_rar($content);
        push @rars, find_rar($_) for grep { -d } glob "$content/CD*";
    }
    elsif ($content =~ /\.rar$/) {
        if (rar_contents($content) > 1) {
            my $dest_dir = $content;
            $dest_dir =~ s/\.rar$//;
            mkdir $dest_dir;
            chdir $dest_dir;
        }
        push @rars, $content;
    }

    $log_values[4] = scalar @rars;
    torrentlog(@log_values);

    if (@rars) {
        my $unrar_start = time;
        for my $rar (@rars) {
            my $rar_path = catfile($config->{complete_dir}, $rar);
            exit if fork; # let's not hold up rtorrent while we unrar
            system "unrar x -o+ '$rar_path'" and die "Can't unrar '$rar': $!";
        }
        my $unrar_finish = time;

        @log_values = ('unrar', $unrar_start, $unrar_finish, scalar @rars, $torrent);
        torrentlog(@log_values);
    }
}

sub torrentlog
{
    my @values = @_;
    open my $log, ">>", $config->{log_file} or die "Can't open torrentlog: $!";
    print $log join(',', @values), "\n";
    close $log;
}

sub find_rar {
    my ($dir) = @_;

    my @files = sort grep { chomp; /\.(?:r(?:ar|\d+)|\d+)$/ } qx/find '$dir' -maxdepth 1/;
    if (@files) {
        my $target = $files[0];
        if (my ($rar) = first { /\.rar$/ } @files) {
            $target = $rar;
        }

        return $target if rar_contents($target) == 1;
    }
    return;
}

sub rar_contents {
    my ($rar) = @_;
    return split /^/, qx/unrar lb '$rar'/;
}

=head1 NAME

torren-queue - Do various stuff when torrents are enqueued/finished

=head1 AUTHOR

Hinrik Örn Sigurðsson <hinrik.sig@gmail.com>
Ævar Arnfjörð Bjarmason <avar@cpan.org>
